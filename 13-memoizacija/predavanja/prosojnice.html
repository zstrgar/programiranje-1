<!DOCTYPE html>
<html>
<head>
<title>Memoizacija v OCamlu</title>
<meta charset="utf-8">
<link rel="stylesheet" href="../../pomozno/prosojnice.css" />
</head>
<body>
<textarea id="source">

class: center, middle

# Memoizacija

## Programiranje 1

---

### Kadar:

1. ima naloga **lastnost optimalne podstrukture** in
2. se podnaloge **prekrivajo**,

.center[![](slike/dinamicno-programiranje.png)]

### govorimo o **dinamičnem programiranju**

---

### Memoizacija v OCamlu

.source[```
let kvadrati = Hashtbl.create 512
let mem_kvadrat x =
  match Hashtbl.find_opt kvadrati x with
  | Some y -> y
  | None ->
      print_endline (string_of_int x);
      let y = x * x in
      Hashtbl.add kvadrati x y;
      y
```]

---

### Memoizacija rekurzivne funkcije

.source[```
let fibonaccijeva_st = Hashtbl.create 512
let rec mem_fib n =
  match Hashtbl.find_opt fibonaccijeva_st n with
  | Some fn -> fn
  | None ->
      print_endline (string_of_int n);
      let fn =
        match n with
        | 0 | 1 -> n
        | n -> mem_fib (n - 1) + mem_fib (n - 2)
      in
      Hashtbl.add fibonaccijeva_st n fn;
      fn
```]

---

### Memoizacija **poljubne** funkcije

.source[```
let memoiziraj f =
  let rezultati = Hashtbl.create 512 in
  let mem_f x =
    match Hashtbl.find_opt rezultati x with
    | None ->
        let y = f x in
        Hashtbl.add rezultati x y;
        y
    | Some y ->
        y
  in
  mem_f
```]

---

### Memoizacija **poljubne** funkcije

.source[```
let kvadrat x =
  print_endline ("Računam " ^ string_of_int x);
  x * x

let mem_kvadrat = memoiziraj kvadrat
```]

.terminal[```
# mem_kvadrat 10;;
Računam 10
- : int = 100
# mem_kvadrat 10;;
- : int = 100
# mem_kvadrat 5;;
Računam 5
- : int = 25
# mem_kvadrat 5;;
- : int = 25
```]

---

### Memoizacija rekurzivnih funkcij **ne dela** v redu

.source[```
let rec fib n =
  print_int n;
  match n with
  | 0 | 1 -> n
  | n -> fib (n - 1) + fib (n - 2)
let mem_fib = memoiziraj fib
```]

.terminal[```
# mem_fib 5;;
531201420131201- : int = 5
# mem_fib 5;;
- : int = 5
# mem_fib 6;;
6420131201531201420131201- : int = 8
```]

#### OCaml za razliko od Pythona ni dinamičen jezik

---

### V izračun želimo vriniti drugo funkcijo, zato rekurzivno definicijo **razbijemo** na dva dela

.source[```
let odviti_fib f n =
  print_int n;
  match n with
  | 0 | 1 -> n
  | n -> f (n - 1) + f (n - 2)

let rec fib n = odviti_fib fib n
```]

### Funkcija `fib` je **fiksna točka** funkcije `odviti_fib`

---

### Vozel lahko zavežemo tudi v splošnem

.source[```
let zavezi_vozel odviti_f =
  let rec f x =
    odviti_f f x
  in
  f

let fib = zavezi_vozel (fun fib n ->
  match n with
  | 0 | 1 -> n
  | n -> fib (n - 1) + fib (n - 2)
)
```]

---

### Sedaj v kodo vrinemo še memoizacijo

.source[```
let memoiziraj_rec odviti_f =
  let rezultati = Hashtbl.create 512 in
  let rec mem_f x =
    match Hashtbl.find_opt rezultati x with
    | None ->
        let y = odviti_f mem_f x in
        Hashtbl.add rezultati x y;
        y
    | Some y ->
        y
  in
  mem_f
```]

---

class: question, middle, center

## Najdaljše skupno podzaporedje


### **4**, 5, 2, 1, 9, 0, **4**, **0**, 1, **0**, 7, 5, **9**, 8, **5**, 2, **1**, 6, 8

### 9, 3, **4**, 3, 3, **4**, 6, 3, **0**, **0**, **9**, 2, **5**, **1**, 7, 7, 7, 0, 3

---

class: center, middle, question

## Število stolpov izmenjujočih barv

.center[![](slike/stolpi-kocke.png)]

.center[![](slike/stolpi-4.png)]

</textarea>
<script src="../../pomozno/prosojnice.js"></script>
</body>
</html>
